" ==========================================================================
" Vim Configuration
"
" Author:   Kashun YOSHIDA
" Platform: Windows, Linux, MacOSX
" NOTE:     To use this, copy to your home directory.
" ==========================================================================

" Basic Settings {{{
" Init {{{
if !1 | finish | endif          " Skip if the live Vim is vim-tiny or vim-small
let s:is_cmd = has('win32') && !has('gui_running')  " For command prompt on Windows
" }}}

" Encoding {{{
let &termencoding = &encoding   " Encoding used for the terminal
set encoding=utf-8              " Sets the character encoding used inside Vim
scriptencoding utf-8            " Specify encoding used in the script
set fileformats=unix,dos,mac    " This gives the end-of-line (<EOL>) formats
" }}}

" Disable {{{
if !has('mac') | set guioptions=M | endif " Disable menu.vim & Use console dialog
set noerrorbells novisualbell t_vb=   " Disable annoying bells
set noswapfile nobackup nowritebackup " Doesn't generate backup files

" Disable default plugins
let g:loaded_tar               = 1
let g:loaded_tarPlugin         = 1
" let g:loaded_gzip              = 1
" let g:loaded_zip               = 1
" let g:loaded_zipPlugin         = 1
let g:loaded_rrhelper          = 1
" let g:loaded_2html_plugin      = 1
let g:loaded_vimball           = 1
let g:loaded_vimballPlugin     = 1
let g:loaded_getscript         = 1
let g:loaded_getscriptPlugin   = 1
let g:loaded_logipat           = 1
let g:loaded_matchparen        = 1
" let g:loaded_netrw             = 1
" let g:loaded_netrwPlugin       = 1
" let g:loaded_netrwSettings     = 1
" let g:loaded_netrwFileHandlers = 1
" }}}
" }}}

" Misc {{{
" Appearance
set ambiwidth=double            " Use twice the width of ASCII characters
set display=lastline            " Enable view long line
set formatoptions& formatoptions+=mMj  " Support Japanese to join lines
set helpheight=999              " Open help to fill the screen
set helplang=ja,en              " Comma separated list of languages.
set history=200                 " History
set iskeyword& iskeyword+=-     " Include '-' to word
set list listchars=tab:>-,trail:-  " Visualize character
" set nomodeline
set matchpairs& matchpairs+=<:>,「:」,【:】,『:』,《:》,〈:〉,（:） " Add support brackets
set scrolloff=5                 " Keep above and below the cursor
" set spell spelllang=en_us,cjk
" set synmaxcol=200               " Only highlight the first 200 columns
set ttimeout timeoutlen=300 ttimeoutlen=50      " Speedup for ESC
set updatetime=100
set virtualedit=block           " expand selection area for visual mode

" Edit
set backspace=indent,eol,start  " Can erase everything in the back space
if has('clipboard')
  set clipboard=unnamed,unnamedplus " Use the OS clipboard
endif
set complete& complete+=k complete-=i  " Specifies how keyword completion
set completeopt=menuone         " Force the user to select one
set iminsert=0 imsearch=-1      " Insert, Search mode: ime setting
set tags=./tags;                " Refer to projects root tags file

" Indent&Tab
set cindent        " Enables automatic C program indenting.
set tabstop=8      " Width on the screen of the tab
set softtabstop=4  " Number of spaces in the file space is the corresponding
set expandtab      " No expand tabs to spaces (expandtab: expand)
set shiftwidth=4   " Shift move width
set smarttab       " Indent by the number of 'shiftwidth'.

" Search
set hlsearch       " Highlight searches
nohlsearch         " Prevent the highlights when reload
set incsearch      " Do incremental searching
set ignorecase     " Ignore case when searching
set smartcase      " No ignorecase if Uppercase char present

" Command-line
set cmdheight=2                 " Command-line height
set wildmenu wildmode=longest:full,full " Command-line completion

" Statusline
" %F:                             FileName(Relative Pathname)
" %m:                             Fix flag([+] or [-])
" %r:                             Read only flg([RO])
" %h:                             Help buffer
" %w:                             Preview window flag
" %=:                             Separated left & right item
" [%{&ff}]                        FileFormat
" [%{&enc}]                       FileEncording
" [%{strlen(&ft)?&ft:'no\ ft'}]   FileType
" %{printf('%'.(len(line('$'))+2).'d/%d',line('.'),line('$'))}
" %v:                             Virtual column number
" %P:                             Cursor-Now Column/Total Number
" set ruler
set laststatus=2                " Always display status bar
let &statusline="%<%F%m%r%h%w%=[%{&ff}][%{&enc}][%{strlen(&ft)?&ft:'no\ ft'}]"
  \ . "%{printf('%'.(len(line('$'))+2).'d/%d',line('.'),line('$'))}%4v\ %P"
" }}}

" netrw {{{
let g:netrw_altv = 1  " Open 'Horizontal split' to the right
let g:netrw_banner = 0  " Hide information displayed at the top
let g:netrw_liststyle = 3   " Change display format to TreeView
let g:netrw_winsize = 15  " Open at 15% size when opened by split
" }}}

" KeyMaping {{{
let g:mapleader = "\<Space>"
" nnoremap <silent> <ESC> <ESC>:nohlsearch<CR>
" Escape automatically according to the situation question and backslash.
cnoremap <expr> / getcmdtype() == '/' ? '\/' : '/'
cnoremap <expr> ? getcmdtype() == '?' ? '\?' : '?'
" Adjust the window size to the window time-division. Shift + arrow key.
nnoremap <silent> <S-Left>  :5wincmd <<CR>
nnoremap <silent> <S-Right> :5wincmd ><CR>
nnoremap <silent> <S-Up>    :5wincmd -<CR>
nnoremap <silent> <S-Down>  :5wincmd +<CR>
" Even text wrapping movement by j or k, is modified to act naturally.
nnoremap j gj
nnoremap k gk
" MRU
nnoremap <Leader>e  :<C-u>/ oldfiles<Home>browse filter /
" Like CTRL-], but use ":tjump" instead of ":tag".
nnoremap <C-]> g<C-]>zz
" Like fzf.vim for quickfix-window
nnoremap <silent> <C-k> :cprevious<CR>
nnoremap <silent> <C-j> :cnext<CR>
" To move unuse arrow key
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>
cnoremap <C-b> <Left>
cnoremap <C-f> <Right>
cnoremap <C-a> <Home>
cnoremap <C-e> <End>
cnoremap <C-d> <Del>

" Enable Bracketed Paste Mode in vim when using tmux (Vim 9.0+ native support)
if &term =~# 'screen'
  let &t_BE = "\e[?2004h"
  let &t_BD = "\e[?2004l"
  exec "set t_PS=\e[200~"
  exec "set t_PE=\e[201~"
endif
" Windows Subsystem for Linux で、ヤンクでクリップボードにコピー
if system('uname -a | grep microsoft') != ''
  augroup myYank
    autocmd!
    autocmd TextYankPost * :call system('clip.exe', @")
  augroup END
endif
" }}}

" Commands {{{
" ReOpen Encoding {{{
command! Cp932 edit ++enc=cp932
command! Eucjp edit ++enc=euc-jp
command! Iso2022jp edit ++enc=iso-2022-jp
command! Utf8 edit ++enc=utf-8
command! Jis Iso2022jp
command! Sjis Cp932
" }}}

if !exists(':DiffOrig')
  command DiffOrig vert new | set bt=nofile | r # | 0d_ | diffthis
    \ | wincmd p | diffthis
endif

command! -nargs=0 BlankLineTrim :v/./d
command! -nargs=+ Grep execute 'grep ' . shellescape(<q-args>)
" Change to git project root directory
command! -nargs=0 Root cd `git rev-parse --show-toplevel`
" }}}

" Remove end of line white space {{{
command! -range=% WhitespaceTrim call <SID>WhitespaceTrim(<line1>, <line2>)
function! s:WhitespaceTrim(line1, line2) abort
  let l:save_cursor = getpos('.')
  silent! execute ':' . a:line1 . ',' . a:line2 . 's/\\\@<!\s\+$//'
  call setpos('.', l:save_cursor)
endfunction " }}}

" AutoCommand {{{
" init augroup (Write the following in order of less than patch 7.4.2103)
augroup MyAutoCmd
  autocmd!
augroup END
autocmd MyAutoCmd QuickFixCmdPost *grep* cwindow " Auto open quickfix-window
autocmd MyAutoCmd InsertLeave * set iminsert=0 imsearch=-1 nopaste " IME off
" }}}

" Grep settings {{{
if executable('rg')
  let &grepprg='rg --vimgrep --hidden --fixed-strings -g "!{**/node_modules/*,**/.git/*,*.db}"'
  set grepformat=%f:%l:%c:%m
elseif executable('grep')
  let s:excludefiles='*tags,*svg,*bundle*js,*png,*jpeg,*jpg,swagger-ui.js,*lock*,*.db'
  let s:excludedirs='*git,*venv*,node_modules,static_files,tmp,dist*,data,build'
  \ . ',*mypy_cache,*tox*,*.pytest_cache,*.serverless,test-results,screenshots,locale'
  let &grepprg='grep -rnIH --exclude-dir={' . s:excludedirs . '} --exclude={' . s:excludefiles .'}'
  set wildignore& wildignore+=s:excludefiles
endif " }}}

" Settings available in Vim 9.0+ {{{
set undofile undodir=$HOME/.cache/tmp
set breakindent
set nofixeol
set signcolumn=yes
" }}}

" Plugins List {{{
" Plugins will be downloaded under the specified directory.
let s:plug_dir = '~/.cache/plugged'
let s:is_async = (v:version >= 800 && has('timers') && has('job') && has('channel'))
let s:executable_nodejs = (executable('node') || executable('nodejs')) && executable('npm')
if !&diff && isdirectory(expand(s:plug_dir))
  call plug#begin(s:plug_dir)
  " Declare the list of plugins.
  Plug 'junegunn/vim-plug'
  " Fizzy Finder
  if has('win32')
    Plug 'ctrlpvim/ctrlp.vim',          {'on': ['CtrlP', 'CtrlPBuffer', 'CtrlPMRUFiles']}
    Plug 'rhysd/vim-color-spring-night'
  else
    Plug 'junegunn/fzf',                {'dir': '~/.fzf', 'do': './install --all' }
    Plug 'junegunn/fzf.vim'
  endif
  " Git
  Plug 'lambdalisue/gina.vim'
  Plug 'mattn/gist-vim',                {'on': 'Gist'}
    \ | Plug 'mattn/webapi-vim',        {'on': 'Gist'}
  let g:gist_per_page_limit = 100
  let g:gist_post_private = 1
  " Snippets
  if has('python3') && !has('win32')
    Plug 'SirVer/ultisnips',              {'on': [], 'for': 'go'}
    Plug 'honza/vim-snippets',            {'on': []}
  endif
  " LSP
  if s:is_async && s:executable_nodejs
    Plug 'neoclide/coc.nvim', {'branch': 'release'}
  endif
  " FileType
  Plug 'thinca/vim-ft-rst_header',      {'for': 'rst'}
  Plug 'rhysd/vim-gfm-syntax',          {'for': 'markdown'}
  Plug 'sheerun/vim-polyglot'
  let g:polyglot_disabled = ['markdown', 'python', 'java', 'csv']
  Plug 'Shougo/context_filetype.vim'    " For vue
  Plug 'editorconfig/editorconfig-vim'
  let g:EditorConfig_exclude_patterns = ['fugitive://.*', 'scp://.*']
  " Formater
  Plug 'junegunn/vim-easy-align',       {'on': '<Plug>(EasyAlign)'}
  Plug 'tyru/caw.vim',                  {'on': ['<Plug>(caw:prefix)', '<Plug>(caw:hatpos:toggle)']}
  " Memo
  Plug 'glidenote/memolist.vim',        {'on': ['MemoGrep', 'MemoList', 'MemoNew']}
  " Reference & View
  Plug 'previm/previm',                 {'on': 'PrevimOpen', 'for': ['rst', 'markdown']}
  Plug 'tyru/open-browser.vim',         {'on': ['PrevimOpen', '<Plug>(openbrowser-smart-search)'], 'for': ['rst', 'markdown']}
  Plug 'vim-jp/vimdoc-ja'
  Plug 'kana/vim-operator-user'
  Plug 'tyru/operator-camelize.vim'
  map ,c <plug>(operator-camelize-toggle)

  " List ends here. Plugins become visible to Vim after this call.
  call plug#end() " }}}

  " Plugins Config {{{
  function! s:installed_plugin(plug_name) abort
    return empty(globpath(&rtp, a:plug_name))
  endfunction
  " Fizzy Finder {{{
  if s:installed_plugin('ctrlp.vim') && has('win32') " {{{
    nnoremap <C-p> :<C-u>CtrlP<CR>
    nnoremap <silent> <Leader>fb :<C-u>CtrlPBuffer<CR>
    nnoremap <silent> <Leader>fm :<C-u>CtrlPMRUFiles<CR>
    let g:ctrlp_use_caching   = 0  " no cache
    let g:ctrlp_show_hidden   = 1  " Show dotfiles
    let g:ctrlp_key_loop      = 1  " Support multi-byte character
    let g:ctrlp_match_window  = 'bottom,order:btt,min:1,max:10000,results:50'
    let g:ctrlp_custom_ignore = {
      \ 'dir' : '\v[\/]\.(git|hg|svn|venv|mypy_cache)$',
      \ 'file': '\v\.(exe|so|dll|swp|zip|gz|jpg|png|gif|pyc)$',
      \ 'link': 'SOME_BAD_SYMBOLIC_LINKS',
      \ }
    let g:ctrlp_user_command = {
    \ 'types': {
    \ 1: ['.git', 'cd %s && git ls-files'],
    \ 2: ['.hg', 'hg --cwd %s locate -I .'],
    \ },
    \ }
    " }}}
    " \ 'fallback': 'pt --follow -g='
  endif
  if s:installed_plugin('fzf.vim') && !has('win32') " {{{
    nnoremap <C-p> :<C-u>Files<CR>
    nnoremap <silent> <Leader>fb :<C-u>Buffers<CR>
    nnoremap <silent> <Leader>fm :<C-u>History<CR>
    command! -bang -nargs=? -complete=dir Files
    \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)
    command! -bang -nargs=? -complete=dir GFiles
    \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)
  endif " }}}
  " }}}

  if s:installed_plugin('coc.nvim') && s:is_async && s:executable_nodejs " {{{
    let g:coc_global_extensions = [
      \ 'coc-prettier',
      \ 'coc-snippets',
      \ 'coc-json',
      \ 'coc-tsserver',
      \ 'coc-eslint',
      \ 'coc-html',
      \ 'coc-htmldjango',
      \ 'coc-css',
      \ 'coc-explorer',
      \ 'coc-git',
      \ 'coc-sh',
      \ 'coc-go',
      \ 'coc-docker',
      \ 'coc-sql',
      \ 'coc-diagnostic',
      \ 'coc-highlight',
      \ 'coc-basedpyright',
      \ '@yaegassy/coc-volar',
      \ ]
    autocmd MyAutoCmd FileType * call lsp#Configure()
    " Explorer
    nnoremap <silent> ge :CocCommand explorer<CR>
    " Use <c-space> to trigger completion.
    inoremap <silent><expr> <c-space> coc#refresh()

    hi CocInlayHint guifg=#e6e6e6 guibg=#505050 ctermbg=LightRed
  endif " }}}

  if s:installed_plugin('memolist.vim') " {{{
    let s:memo_path = ((has('win32') || has('mac')) ? ' (個人用)' :'')
    let g:memolist_path = '~/Dropbox' . s:memo_path . '/memo'
    let g:memolist_memo_suffix = 'md'
    let g:memolist_template_dir_path = '~/.vim/template/memolist'
    let g:memolist_ex_cmd = (has('win32') ? 'CtrlP': 'Files')
    nnoremap <silent> <Leader>ml :MemoList<CR>
    nnoremap <silent> <Leader>mn :MemoNew<CR>
    nnoremap <silent> <Leader>mg :MemoGrep<CR>
  endif " }}}

  if s:installed_plugin('caw.vim') " {{{
    function! InitCaw() abort
      if !&l:modifiable
        silent! nunmap <buffer> gc
        silent! xunmap <buffer> gc
        silent! nunmap <buffer> gcc
        silent! xunmap <buffer> gcc
        silent! nunmap <buffer> <Leader>c
        silent! xunmap <buffer> <Leader>c
      else
        nmap <buffer> gc <Plug>(caw:prefix)
        xmap <buffer> gc <Plug>(caw:prefix)
        nmap <buffer> gcc <Plug>(caw:hatpos:toggle)
        xmap <buffer> gcc <Plug>(caw:hatpos:toggle)
        nmap <buffer> <Leader>c <Plug>(caw:hatpos:toggle)
        xmap <buffer> <Leader>c <Plug>(caw:hatpos:toggle)
      endif
    endfunction
    autocmd MyAutoCmd FileType * call InitCaw()
  endif " }}}

  if s:installed_plugin('vim-easy-align') " {{{
    " Start interactive EasyAlign in visual mode (e.g. vipga)
    xmap ga <Plug>(EasyAlign)
    " Start interactive EasyAlign for a motion/text object (e.g. gaip)
    nmap ga <Plug>(EasyAlign)
  endif " }}}

  if s:installed_plugin('previm') " {{{
    let g:previm_disable_vimproc = 1
    let g:previm_show_header = 0
    autocmd MyAutoCmd User previm doautocmd Previm FileType
  endif " }}}

  if s:installed_plugin('open-browser.vim') " {{{
    let g:netrw_nogx = 1 " disable netrw's gx mapping.
    nmap gx <Plug>(openbrowser-smart-search)
    vmap gx <Plug>(openbrowser-smart-search)
  endif " }}}

  if has('python3') && !has('win32')
    augroup load_insert_snippet_plugins " {{{
      autocmd!
      autocmd InsertEnter * call plug#load(
    \ 'ultisnips',
    \ 'vim-snippets',
    \ )
    \ | autocmd! load_insert_snippet_plugins
    augroup END " }}}
  endif
else
  filetype plugin indent on       " Load plugins according to detected filetype.
  syntax on                       " Enable syntax highlight
endif "}}}

" Highlights {{{
" if has('gui_running')
hi DiffAdd term=bold ctermbg=LightRed guibg=LightRed
hi DiffChange term=bold ctermbg=LightCyan guibg=LightCyan
hi DiffDelete term=bold ctermfg=Red ctermbg=LightMagenta gui=bold guifg=Red guibg=LightMagenta
hi DiffText term=reverse cterm=bold ctermbg=Blue gui=bold guibg=Blue
hi Search ctermfg=white ctermbg=blue guifg=#ffffff guibg=#0000ff
" endif " }}}

" Load local settings {{{
if filereadable(expand('~/.vimrc.local'))
  source ~/.vimrc.local
endif
" }}}

" vim: tw=78 et sw=2 foldmethod=marker
