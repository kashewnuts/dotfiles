" ==========================================================================
" Vim GUI Configuration
"
" Author:   Kashun YOSHIDA
" Platform: Windows, Linux, MacOSX
" NOTE:     To use this, copy to your home directory.
" ==========================================================================

" Font {{{
if has('win32')
  let &guifont='Ricty_Diminished:h14'
elseif has('mac')
  let &guifont='Ricty_Diminished:h16'
else
  let &guifont='Monospace 12'
  let &guifontwide='Monospace 12'
endif
" }}}

" Colorscheme {{{
try
  colorscheme molokai
catch
  colorscheme morning
  hi DiffAdd term=bold ctermbg=LightRed guibg=LightRed
  hi DiffChange term=bold ctermbg=LightCyan guibg=LightCyan
  hi DiffDelete term=bold ctermfg=Red ctermbg=LightMagenta gui=bold guifg=Red guibg=LightMagenta
  hi DiffText term=reverse cterm=bold ctermbg=Blue gui=bold guibg=Blue
endtry " }}}

" Hack #120: Store the location and size of the window by gVim {{{
let s:save_window_file = expand('~/.vimwinpos')
augroup SaveWindow
  autocmd!
  autocmd VimLeavePre * call s:save_window()
  function! s:save_window()
    let options = [
      \ 'set columns=' . &columns,
      \ 'set lines=' . &lines,
      \ 'winpos ' . getwinposx() . ' ' . getwinposy(),
      \ ]
    call writefile(options, s:save_window_file)
  endfunction
augroup END

if filereadable(s:save_window_file)
  execute 'source ' . s:save_window_file
endif " }}}

" Semitransparency {{{
if exists('&transparency')
  if has('win32')
    augroup Semitransparency
      autocmd!
      autocmd GUIEnter * set transparency=220
    augroup END
  elseif has('mac') || has('macunix') || has('gui_macvim')
    set transparency=20
  endif
endif " }}}

" vim: tw=78 et sw=2 foldmethod=marker
