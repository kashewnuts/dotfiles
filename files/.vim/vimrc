" ==========================================================================
" Vim Configuration
"
" Author:   Kashun YOSHIDA
" Platform: Windows, Linux, MacOSX
" NOTE:     To use this, copy to your home directory.
" ==========================================================================

" Basic Settings {{{
if !1 | finish | endif          " Skip if the live Vim is vim-tiny or vim-small
let s:is_cmd = has('win32') && !has('gui_running')  " For command prompt on Windows

" Encoding {{{
" Sets the character encoding used inside Vim
let &termencoding = &encoding
let &encoding = s:is_cmd ? 'cp932' : 'utf-8'
scriptencoding utf-8            " Specify encoding used in the script
set fileformats=unix,dos,mac    " This gives the end-of-line (<EOL>) formats
" }}}

" Disable {{{
set guioptions=M                      " Disable menu.vim & Use console dialog
set noerrorbells novisualbell t_vb=   " Disable annoying bells
set noswapfile nobackup nowritebackup " Doesn't generate backup files

" Disable defalut plugins
let g:loaded_gzip              = 1
let g:loaded_tar               = 1
let g:loaded_tarPlugin         = 1
let g:loaded_zip               = 1
let g:loaded_zipPlugin         = 1
let g:loaded_rrhelper          = 1
" let g:loaded_2html_plugin      = 1
let g:loaded_vimball           = 1
let g:loaded_vimballPlugin     = 1
let g:loaded_getscript         = 1
let g:loaded_getscriptPlugin   = 1
let g:loaded_logipat           = 1
" let g:loaded_matchparen        = 1
" let g:loaded_netrw             = 1
" let g:loaded_netrwPlugin       = 1
" let g:loaded_netrwSettings     = 1
" let g:loaded_netrwFileHandlers = 1

" Don't read $VIM/(g)vimrc
if has('kaoriya')
  function! s:disable_source(vimrc_local, message)
    let l:vimrc_local = expand($VIM . '/' . a:vimrc_local)
    if !filereadable(l:vimrc_local)
      execute ':redir! >' . l:vimrc_local
      silent! echon a:message
      redir END
    endif
  endfunction
  call s:disable_source('vimrc_local.vim',  'let g:vimrc_local_finish = 1')
  call s:disable_source('gvimrc_local.vim', 'let g:gvimrc_local_finish = 1')
endif " }}}
" }}}

" Misc {{{
" Appearance
set ambiwidth=double            " Use twice the width of ASCII characters
set display=lastline            " Enable view long line
set fillchars=vert:\|,fold:\    " Characters to fill the separators
set helplang=ja,en              " Comma separated list of languages.
set helpheight=999              " Open help to fill the screen
set history=1000                " History
set list listchars=tab:>-,trail:-  " Visualize character
set number                      " Show line number (nonumber: Hide)
set scrolloff=999               " Keep above and below the cursor
set showmatch matchtime=1       " The highlight matching brackets
set matchpairs& matchpairs+=<:> " To support brackets add a pair of '<' and '>'

" Edit
set backspace=indent,eol,start  " Can erase everything in the back space
if has('clipboard') | set clipboard=unnamed | endif " Use the OS clipboard
set complete& complete+=k       " Specifies how keyword completion
set completeopt=menuone         " Force the user to select one & No preview
set iminsert=0 imsearch=-1      " Insert, Search mode: ime setting
set tags=./tags;                " Refer to projects root tags file

" Format
if has('vim_starting') | set runtimepath+=$VIM/plugins/autofmt | endif
set formatoptions& formatoptions+=mM  " Support Japanese to join lines
set formatexpr=autofmt#japanese#formatexpr()
let autofmt_allow_over_tw=1

" Indent
set autoindent     " Copy indent from current line when starting a new line
set smartindent    " Do smart autoindenting when starting a new line
set copyindent     " Copy the structure of the existing lines indent

" Tab
set tabstop=8      " Width on the screen of the tab
set softtabstop=4  " Number of spaces in the file space is the corresponding
set expandtab      " No expand tabs to spaces (expandtab: expand)
set shiftwidth=4   " Shift move width
set smarttab       " Indent by the number of 'shiftwidth'.

" Search
set hlsearch       " Highlight searches
nohlsearch         " Prevent the highlights when reload
set incsearch      " Do incremental searching
set ignorecase     " Ignore case when searching
set smartcase      " No ignorecase if Uppercase char present

" Command-line
set cmdheight=2                 " Command-line height
set wildmenu wildmode=list:full " Command-line completion

" Statusline
" %F:                             FileName(Relative Pathname)
" %m:                             Fix flag([+] or [-])
" %r:                             Read only flg([RO])
" %h:                             Help buffer
" %w:                             Preview window flag
" %=:                             Separated left & right item
" [%{&ff}]                        FileFormat
" [%{&enc}]                       FileEncording
" [%{strlen(&ft)?&ft:'no\ ft'}]   FileType
" %{printf('%'.(len(line('$'))+2).'d/%d',line('.'),line('$'))}
" %v:                             Virtual column number
" %P:                             Cursor-Now Column/Total Number
set laststatus=2                " Always display status bar
let &statusline="%<%F%m%r%h%w%=[%{&ff}][%{&enc}][%{strlen(&ft)?&ft:'no\ ft'}]"
  \ . "%{printf('%'.(len(line('$'))+2).'d/%d',line('.'),line('$'))}%4v\ %P"

" Terminal
set ttimeout timeoutlen=300 ttimeoutlen=50      " Speedup for ESC
if !has('gui_running')
  set lazyredraw                " Only redraw when necessary.
  set ttyfast                   " Faster redrawing.
endif
" }}}

" KeyMaping {{{
let g:mapleader = "\<Space>"
" nnoremap <silent> <ESC> <ESC>:nohlsearch<CR>
" Escape automatically according to the situation question and backslash.
cnoremap <expr> / getcmdtype() == '/' ? '\/' : '/'
cnoremap <expr> ? getcmdtype() == '?' ? '\?' : '?'
" To Enable filtering the command history
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>
" Adjust the window size to the window time-division. Shift + arrow key.
nnoremap <silent> <S-Left>  :5wincmd <<CR>
nnoremap <silent> <S-Right> :5wincmd ><CR>
nnoremap <silent> <S-Up>    :5wincmd -<CR>
nnoremap <silent> <S-Down>  :5wincmd +<CR>
" Even text wrapping movement by j or k, is modified to act naturally.
nnoremap j gj
nnoremap k gk
" MRU
nnoremap <Leader>e  :<C-u>/ oldfiles<Home>browse filter /
" Like CTRL-], but use ":tjump" instead of ":tag".
nnoremap <C-]> g<C-]>zz
" }}}

" Commands {{{
" ReOpen Encoding {{{
command! Cp932 edit ++enc=cp932
command! Eucjp edit ++enc=euc-jp
command! Iso2022jp edit ++enc=iso-2022-jp
command! Utf8 edit ++enc=utf-8
command! Jis Iso2022jp
command! Sjis Cp932
" }}}

" Remove end of line white space {{{
command! -range=% FixWhitespace call <SID>FixWhitespace(<line1>, <line2>)
function! s:FixWhitespace(line1, line2)
  let l:save_cursor = getpos('.')
  silent! execute ':' . a:line1 . ',' . a:line2 . 's/\\\@<!\s\+$//'
  call setpos('.', l:save_cursor)
endfunction " }}}

" Format JSON
if executable('jq') " {{{
  command! -nargs=? Jq call s:Jq(<f-args>)
  function! s:Jq(...)
    execute '%!jq' (a:0 == 0 ? '.' : a:1)
  endfunction
endif " }}}
" }}}

" AutoCommand {{{
" init augroup (Write the following in order of less than patch 7.4.2103)
augroup MyAutoCmd
  autocmd!
augroup END
autocmd MyAutoCmd QuickFixCmdPost *grep* cwindow " Auto open quickfix-window
autocmd MyAutoCmd InsertLeave * set iminsert=0 imsearch=-1 " IME off
" }}}

" Grep settings {{{
if executable('pt')
  set grepprg=pt    " https://github.com/monochromegane/the_platinum_searcher
elseif executable('grep')
  set grepprg=grep\ -nH
endif " }}}

if v:version >= 704 " {{{
  set undofile undodir=$HOME/.cache/tmp
  set nofixeol
  set breakindent
endif " }}}

" Plugin Config {{{
let s:plugins_path = '~/.vim/rc/plug.vim'
if filereadable(expand(s:plugins_path))
  execute 'source' . s:plugins_path
endif " }}}

" Others {{{
" Essential
filetype plugin indent on       " Load plugins according to detected filetype.
syntax on                       " Enable syntax highlight
if !has('gui_running')
  try
    execute 'colorscheme ' . (s:is_cmd ? 'industry' : 'molokai')
  catch
    try | colorscheme desert | catch | endtry
  endtry
endif

" Load local settings
if filereadable(expand('~/.vimrc.local'))
  source ~/.vimrc.local
endif
" }}}

" vim: tw=78 et sw=2 foldmethod=marker
