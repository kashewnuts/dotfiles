" ==========================================================================
" Vim Configuration
"
" Author:   Kashun YOSHIDA
" Platform: Windows, Linux, MacOSX
" NOTE:     To use this, copy to your home directory.
" ==========================================================================

" Basic Settings {{{
if !1 | finish | endif          " skip if the live Vim is vim-tiny or vim-small
if &compatible && has('vim_starting')
  set nocompatible              " For dein.vim
endif

" Encoding {{{
if has('win32') && !has('gui_running')
  let &termencoding = &encoding
  set encoding=cp932
else
  set encoding=utf-8
endif
scriptencoding utf-8            " Specify encoding used in the script
set fileformats=unix,dos,mac    " This gives the end-of-line (<EOL>) formats
" }}}

" GUI
set guifont=Ricty\ Diminished:h11
set guioptions=Mc               " Disable menu.vim & Use console dialog

" Disable {{{
set noerrorbells novisualbell t_vb=   " Disable annoying bells
set noswapfile nobackup nowritebackup " Doesn't generate a backup file
set noloadplugins                     " Disable the loading of plugins.

" Don't read $VIM/(g)vimrc
if has('kaoriya')
  function! s:disable_source(vimrc_local, message)
    let l:vimrc_local = expand($VIM . '/' . a:vimrc_local)
    if !filereadable(l:vimrc_local)
      execute ':redir! >' . l:vimrc_local
      silent! echon a:message
      redir END
    endif
  endfunction
  call s:disable_source('vimrc_local.vim',  'let g:vimrc_local_finish = 1')
  call s:disable_source('gvimrc_local.vim', 'let g:gvimrc_local_finish = 1')
endif " }}}
" }}}

" Misc {{{
" Appearance
set ambiwidth=double            " Use twice the width of ASCII characters
set fillchars=vert:\|,fold:\    " Characters to fill the separators
set helpheight=999              " Open help to fill the screen
set history=1000                " history
set list listchars=tab:>-,trail:-,extends:>,precedes:<  " Visualize character
set number                      " Show line number (nonumber: Hide)
set scrolloff=999               " Keep above and below the cursor
set showmatch matchtime=1       " The highlight matching brackets

" Edit
set backspace=indent,eol,start  " Can erase everything in the back space
if has('clipboard') | set clipboard=unnamed | endif " Use the OS clipboard
set iminsert=0 imsearch=-1      " Insert, Search mode: ime setting
set matchpairs& matchpairs+=<:> " To support brackets add a pair of '<' and '>'

" Indent
set autoindent     " For smartindent
set smartindent    " Advanced automatic indentation when you made the new line
set copyindent     " copy the indent structure of existing lines

" Tab
set tabstop=4      " Width on the screen of the tab
set softtabstop=4  " Number of spaces in the file space is the corresponding
set expandtab      " noexpand tabs to spaces (expandtab: expand)
set shiftwidth=4   " Shift move width
set smarttab       " Indent by the number of 'shiftwidth'.

" Search
set hlsearch       " highlight searches
set incsearch      " do incremental searching
set ignorecase     " ignore case when searching
set smartcase      " no ignorecase if Uppercase char present

" Cmdline & Statusline
set cmdheight=2                 " Cmdline height
set wildmenu wildmode=list:full " Command-line completion
set display=lastline            " enable view long line
set laststatus=2                " Always display status bar
" Determines the content of the status line.
set statusline=%F%m%r%h%w\%=\[%{&ff}]\[%{strlen(&fenc)?&fenc:&enc}][%{strlen(&ft)?&ft:'no\ ft'}]\[%l-%c/%L]

" Terminal
set lazyredraw                  " Only redraw when necessary.
set ttyfast                     " Faster redrawing.
" }}}

" KeyMaping {{{
" Normal mode: IME off
inoremap <silent> <Esc><Esc>:set iminsert=0<CR>

" Adjust the window size to the window time-division. Shift + arrow key.
nnoremap <silent> <S-Left>  :5wincmd <<CR>
nnoremap <silent> <S-Right> :5wincmd ><CR>
nnoremap <silent> <S-Up>    :5wincmd -<CR>
nnoremap <silent> <S-Down>  :5wincmd +<CR>

" When you move in the search results,
" and in the center of the screen that position.
nnoremap n nzz
nnoremap N Nzz
nnoremap * *zz
nnoremap # #zz
nnoremap g* g*zz
nnoremap g# g#zz

" Turn off the highlight by pressing twice the ESC.
nnoremap <silent> <Esc><Esc> :nohlsearch<CR>

" Escape automatically according to the situation question and backslash.
cnoremap <expr> / getcmdtype() == '/' ? '\/' : '/'
cnoremap <expr> ? getcmdtype() == '?' ? '\?' : '?'

" To Enable filtering the command history
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>

" Even text wrapping movement by j or k, is modified to act naturally.
nnoremap j gj
nnoremap k gk
" }}}

" ReOpen Encoding {{{
command! Cp932 edit ++enc=cp932
command! Eucjp edit ++enc=euc-jp
command! Iso2022jp edit ++enc=iso-2022-jp
command! Utf8 edit ++enc=utf-8
command! Jis Iso2022jp
command! Sjis Cp932
" }}}

" FileType {{{
augroup MyAutoCmd
  autocmd!
  " Grep
  autocmd QuickFixCmdPost *grep* cwindow " Auto open quickfix-window

  " FileType {{{
  " ts   : tabstop
  " sw   : shiftwidth
  " sts  : softtabstop
  " et   : expandtab
  " noet : noexpandtab
  " si   : smartindent
  " cinw : cinwords
  autocmd FileType html       setl ts=4 sw=4 sts=4 et
  autocmd FileType htmldjango setl ts=2 sw=2 sts=2 et
  autocmd FileType javascript setl ts=4 sw=4 sts=4 et
  autocmd FileType js         setl ft=javascript
  autocmd FileType ruby       setl ts=2 sw=2 sts=2 et
  autocmd FileType go         setl ts=4 sw=4 sts=4 noet
  autocmd FileType vim        setl ts=2 sw=2 sts=2 et
  autocmd FileType toml       setl ts=2 sw=2 sts=2 et
  autocmd FileType make       setl ts=4 sw=4 sts=4 noet
  autocmd FileType rst        setl ts=4 sw=4 sts=4 et
  autocmd FileType text       setl ts=4 sw=4 sts=4 et ft=rst
  autocmd FileType gitconfig  setl ts=4 sw=4 sts=4 noet
  autocmd FileType jsp        setl ts=4 sw=4 sts=4 noet
  autocmd FileType java       setl ts=4 sw=4 sts=4 noet
  autocmd FileType python     setl ts=4 sw=4 sts=4 et textwidth=80
  autocmd FileType sql        setl ts=4 sw=4 sts=4 et fenc=shift_jis ff=dos
  autocmd FileType scp        setl ts=4 sw=4 sts=4 noet fenc=shift_jis ff=dos
  autocmd FileType scp        setl suffixesadd& suffixesadd+=.scp
  " }}}

  " When the '#' character in the first line of the newly created,
  " it isn't unindent
  autocmd FileType python inoremap # X#
  " autocmd BufNewFile *.py 0r ~/.vim/template/python.txt
augroup END

" Golang settings {{{
if executable('jvgrep') | set grepprg=jvgrep | endif " }}}

" Java settings {{{
" Syntax highlight
let g:java_highlight_all       = 1
let g:java_highlight_debug     = 1
let g:java_allow_cpp_keywords  = 1
let g:java_space_errors        = 1
let g:java_highlight_functions = 1
" }}}

" PHP settings {{{
let g:php_baselib       = 1
let g:php_htmlInStrings = 1
let g:php_noShortTags   = 1
let g:php_sql_query     = 1
" }}}
" }}}

" After Vim7.4 {{{
if exists('&undofile') | set noundofile | endif  " Don't make *.un~ files
if exists('&nofixeol') | set nofixeol | endif
if exists('&breakindent') | set breakindent | endif
" }}}

" dein.vim {{{
" Cache
let $CACHE = expand('~/.cache')
if !isdirectory(expand($CACHE))
  call mkdir(expand($CACHE), 'p')
endif

" Load dein.vim
let s:root_dein_path = expand('~/.cache/dein')
let s:toml_path = '~/.vim/rc/dein.toml'
let s:toml_lazy_path = '~/.vim/rc/dein_lazy.toml'
if v:version >= 704
      \ && executable('git')
      \ && executable('rsync')
      \ && filereadable(expand(s:toml_path))
      \ && filereadable(expand(s:toml_lazy_path))

  " dein configurations.
  let g:dein#install_progress_type = 'title'
  let g:dein#install_message_type = 'none'
  let g:dein#enable_notification = 1

  " Begin dein.vim
  let s:dein_dir = finddir('dein.vim', '.;')
  if s:dein_dir !=# '' || &runtimepath !~# '/dein.vim'
    if s:dein_dir ==# '' && &runtimepath !~# '/dein.vim'
      let s:dein_dir = s:root_dein_path . '/repos/github.com/Shougo/dein.vim'

      if !isdirectory(s:dein_dir)
        execute '!git clone https://github.com/Shougo/dein.vim' s:dein_dir
      endif
    endif
    set runtimepath& runtimepath+=~/.cache/dein/repos/github.com/Shougo/dein.vim
  endif

  " Read TOML & cache
  if dein#load_state(s:root_dein_path)
    call dein#begin(s:root_dein_path, [$MYVIMRC, s:toml_path, s:toml_lazy_path])
    call dein#load_toml(s:toml_path, {'lazy': 0})
    call dein#load_toml(s:toml_lazy_path, {'lazy' : 1})
    call dein#end()
    call dein#save_state()
  endif

  if !has('vim_starting') && dein#check_install()
    " Installation check.
    call dein#install()
  endif
endif " }}}

" Others {{{
" Essential
filetype plugin indent on       " Load plugins according to detected filetype.
syntax on                       " Enable syntax highlight

" Colorscheme
try
  if has('win32') && !has('gui_running')
    colorscheme industry
  else
    colorscheme molokai
  endif
catch
  try | colorscheme desert | catch | endtry
endtry

" Load local settings
if filereadable(expand('~/.vimrc.local'))
  execute 'source ~/.vimrc.local'
endif
set secure                      " Safely use Vim
" }}}

" vim: tw=78 et sw=2 foldmethod=marker
