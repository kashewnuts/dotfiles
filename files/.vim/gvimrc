" ==========================================================================
" Vim GUI Configuration
"
" Author:   Kashun YOSHIDA
" Platform: Windows, Linux, MacOSX
" NOTE:     To use this, copy to your home directory.
" ==========================================================================

" Basic {{{
" Env
let $VIMHOME=expand('~/.vim')
if !isdirectory($VIMHOME)
  let $VIMHOME=$VIM
endif
let s:is_win = has('win32') || has('win64')
let s:is_mac = has('mac') || has('macunix') || has('gui_macvim')

set guifont=Ricty\ Diminished:h11
try
  colorscheme molokai
catch
  try | colorscheme desert | catch | endtry
endtry " }}}

" Hack #120: Store the location and size of the window by gVim {{{
let s:save_window_file = expand($VIMHOME . '/vimwinpos')
augroup SaveWindow
  autocmd!
  autocmd VimLeavePre * call s:save_window()
  function! s:save_window()
    let options = [
      \ 'set columns=' . &columns,
      \ 'set lines=' . &lines,
      \ 'winpos ' . getwinposx() . ' ' . getwinposy(),
      \ ]
    call writefile(options, s:save_window_file)
  endfunction
augroup END

if filereadable(s:save_window_file)
  execute 'source ' . s:save_window_file
endif " }}}

" Semitransparency {{{
if exists('&transparency')
  if s:is_win
    augroup Semitransparency
      autocmd!
      autocmd GUIEnter * set transparency=220
    augroup END
  elseif s:is_mac
    set transparency=20
  endif
endif " }}}

" vim: tw=78 et sw=2 foldmethod=marker
